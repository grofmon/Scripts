#!/usr/bin/expect -f

################################################################################
# Expect script to telnet to a given 
# ip address and program flash.
# 
# Author          : Montgomery Groff
# Original Author : Vijay.Jayaraman
# Version         : 1.3
# Last Updated    : 10-12-2013
#
# 1.3 - Removed kill command, mtd_update now kill threads
# 1.2 - Removed reboot to start and added kill command
# 1.1 - Added support for reboot before running mtd_update
# 1.0 - Copied tnet script and modified for flashing the box
#
set box_flash_version "1.2"
################################################################################

#puts "box_flash script version: $box_flash_version"

if {$argc != 3} {
        puts "Usage: box_flash <ip address> <model id> <bin file>"
        puts "Ex: box_flash 192.168.1.202 813 xip813_dev.bin"
        exit
}

# Un-comment "log_user 0" for silent operation
log_user 0
set timeout 2
set ip [lindex $argv 0]
set model [lindex $argv 1]
set bin [lindex $argv 2]
set prompt "~ #"

################################################################################
# User customizable variables - Please customize this for your setup.
################################################################################
set ip_address       "192.168.1.1"
set home_dir         "/home/monty/stb"
################################################################################

set mount_cmd        "mount -o nolock $ip_address:$home_dir /home \r"
set cd_cmd           "cd /home/$model \r"
set mtd_update_cmd   "./mtd_update -bin $bin \r"
set reboot_cmd       "reboot;exit \r"; 

for {set x 0} {$x<120} {incr x} {
    # Open a telnet session to a remote server, and wait for a username prompt.
    spawn telnet $ip

    # Expect a timeout and then retry again.
    expect {
        timeout    {continue}
 	"login:"   {puts "Ready to proceed\n"; send "root\r"; break}
    }
}

# Start logging to stdout
log_user 1

# Mount to nfs share on host
expect $prompt
send $mount_cmd

# Change to the appropriate directory
expect $prompt
send $cd_cmd

# Setup the mtd_update command
expect $prompt
send $mtd_update_cmd

# Setup the reboot command
set timeout -1
expect " Update Complete"
send $reboot_cmd

interact
